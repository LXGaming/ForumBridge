apply plugin: "java"
apply plugin: "java-library"
apply plugin: "maven"
apply plugin: "signing"

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "nz.co.lolnet"
archivesBaseName = "forumbridge-common"
version = version

configurations {
    provided {
        compile.extendsFrom(provided)
    }
    
    build.dependsOn("uploadArchives")
    compileJava.dependsOn(":api:build")
}

repositories {
    jcenter()
    maven {
        name = "lolnet"
        url = "https://repo.lolnet.co.nz/maven"
    }
}

dependencies {
    api(project(path: ":api"))
    provided("com.google.guava:guava:27.0-jre")
    provided("me.lucko.luckperms:luckperms-api:4.3")
    provided("nz.co.lolnet:lolnetapi-api:2.0.4")
}

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from {
        (configurations.compile - configurations.provided).findAll({
            it.isDirectory() || it.name.endsWith(".jar")
        }).collect({
            it.isDirectory() ? it : zipTree(it)
        })
    }
}

processResources {
    from("LICENSE")
    rename("LICENSE", "LICENSE-" + archivesBaseName)
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        if (project.hasProperty("maven.url")) {
            mavenDeployer {
                project.logger.lifecycle("Uploading Archives")
                repository(url: project.property("maven.url"))
                
                pom {
                    groupId = project.group
                    artifactId = project.archivesBaseName
                    version = project.version
                    project {
                        name = project.archivesBaseName
                        packaging = "jar"
                        
                        scm {
                            url = "https://github.com/lolnetnz/ForumBridge"
                            connection = "scm:git:https://github.com/lolnetnz/ForumBridge.git"
                            developerConnection = "scm:git:git@github.com:lolnetnz/ForumBridge.git"
                        }
                        
                        issueManagement {
                            system = "GitHub Issues"
                            url = "https://github.com/lolnetnz/ForumBridge/issues"
                        }
                        
                        licenses {
                            license {
                                name = "Apache License 2.0"
                                url = "https://opensource.org/licenses/Apache-2.0"
                                distribution = "repo"
                            }
                        }
                    }
                }
            }
        }
    }
}

task signJar {
    doFirst {
        if (!project.hasProperty("signing.keyStorePath") || !project.hasProperty("signing.secretKeyRingFile")) {
            project.logger.warn("========== [WARNING] ==========")
            project.logger.warn("")
            project.logger.warn("   This build is not signed!   ")
            project.logger.warn("")
            project.logger.warn("========== [WARNING] ==========")
            throw new StopExecutionException()
        }
    }

    doLast {
        configurations.archives.allArtifacts.files.each {
            ant.signjar(
                jar: it,
                alias: project.property("signing.alias"),
                storepass: project.property("signing.keyStorePassword"),
                keystore: project.property("signing.keyStorePath"),
                keypass: project.property("signing.keyStorePassword"),
                preservelastmodified: project.property("signing.preserveLastModified"),
                tsaurl: project.property("signing.timestampAuthority"),
                digestalg: project.property("signing.digestAlgorithm")
            )
            project.logger.lifecycle("JAR Signed: " + it.name)

            signing.sign(it)
            project.logger.lifecycle("PGP Signed: " + it.name)
        }
    }
}