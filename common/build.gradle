plugins {
    id "com.jfrog.bintray" version "1.8.5"
    id "java"
    id "java-library"
    id "maven-publish"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "net.creationreborn"
archivesBaseName = "forumbridge-common"
version = version

configurations {
    provided {
        compile.extendsFrom(provided)
    }

    compileJava.dependsOn(":api:build")
}

repositories {
    jcenter()
    maven {
        name = "creation-reborn"
        url = "https://dl.bintray.com/creation-reborn/maven"
    }
}

dependencies {
    api(project(path: ":api"))
    implementation("com.google.guava:guava:29.0-jre")
    implementation("net.luckperms:api:5.1")
    api("net.creationreborn:crapi:2.2.3")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    publications = ["mavenPublication"]
    pkg {
        repo = "maven"
        name = "ForumBridge"
        userOrg = "creation-reborn"
        licenses = ["Apache-2.0"]
        vcsUrl = "https://github.com/creation-reborn/ForumBridge.git"
        publish = true
        version {
            name = project.version
            released = new Date()
        }
    }
}

bintrayUpload {
    onlyIf { System.getenv("BINTRAY_KEY") != null }
    onlyIf { System.getenv("BINTRAY_USER") != null }
    onlyIf { System.getenv("TRAVIS_BUILD_NUMBER") != null }
    onlyIf { System.getenv("TRAVIS_JOB_NUMBER") ==~ /\d+\.1$/ }
    onlyIf { System.getenv("TRAVIS_PULL_REQUEST") == "false" }
}

javadoc {
    failOnError = false
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId archivesBaseName
            version version
            artifact javadocJar
            artifact sourcesJar
        }
    }
}